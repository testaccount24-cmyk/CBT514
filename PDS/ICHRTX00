//... YOUR JOB CARD HERE ...
//*
//* SAMPLE OF ICHRTX00 TO BLOCK UPDATE ACCESS TO CERTAIN VOLUME(S)
//*
//* WRITTEN BY LARRY WILLIAMS (EMAIL: LARRYW@IX.NETCOM.COM)
//*
//* ONLY ONE LINE (MARKED <--- CHANGE) SHOULD NEED TO BE UPDATED
//* TO MAKE THIS JCL WORK - THE CODE WILL ALSO WORK FINE IF IEV90
//* IS USED INSTEAD.
//*
//ASMLKED  PROC OUTDS='UNKNOWN.LIBRARY'
//ASM      EXEC PGM=ASMA90,REGION=6144K,PARM='NOOBJ,DECK,TERM,RENT'
//SYSLIB   DD  DSN=SYS1.MACLIB,DISP=SHR,DCB=BLKSIZE=32720
//         DD  DSN=SYS1.MODGEN,DISP=SHR
//SYSUT1   DD  DSN=&&SYSUT1,UNIT=SYSALLDA,SPACE=(CYL,(5,5))
//SYSPRINT DD  SYSOUT=*
//SYSTERM  DD  SYSOUT=*
//SYSPUNCH DD  DSN=&&OBJSET,UNIT=SYSALLDA,SPACE=(CYL,(1,1),RLSE),
//             DISP=(NEW,PASS)
//*
//LKED     EXEC PGM=IEWL,REGION=2048K,COND=(0,NE),
//         PARM='LIST,MAP,XREF,RENT,NCAL'
//SYSLMOD  DD  DISP=SHR,DSN=&OUTDS
//SYSUT1   DD  DSN=&&SYSUT1,UNIT=SYSALLDA,SPACE=(1024,(50,20))
//SYSPRINT DD  SYSOUT=*
//SYSLIN   DD  DSN=&&OBJSET,DISP=(OLD,DELETE,DELETE)
//         DD  DDNAME=SYSIN
//         PEND
//*
//STEP0001 EXEC ASMLKED,OUTDS='USER.LPALIB.HERE' <--- CHANGE
//ASM.SYSIN  DD *
ICHRTX00 TITLE 'ICHRXT00 - SAF ROUTER EXIT'
***********************************************************************
*                                                                     *
*    MODULE NAME:  ICHRTX00 - SAF ROUTER EXIT                         *
*                                                                     *
*    DESCRIPTIVE NAME:  INTERCEPT PRE-RACF CALLS, IF UPDATE ACCESS    *
*    TO DATASET ON HISTXX PACK, VALIDATE ACCESS.                      *
*                                                                     *
*    FUNCTION AND OPERATION:                                          *
*             IF ALLOCATE DATASET, OR ATTEMPT TO UPDATE DATASET ON    *
*             HISTXX PACK, LOOK FOR SYSPROG ID OR OTHER SUPERUSER     *
*             AS USERID, IF NOT FAIL CALL.                            *
*                                                                     *
*    DEPENDENCIES:  ASSEMBLE ASSEMBLER H OR HIGHER ONLY               *
*                                                                     *
*    RESTRICTIONS:  STANDARD RESTRICTIONS FOR ICHRTX00, FOR EXAMPLE   *
*                   NO GETMAIN FOR SAVEAREA                           *
*                                                                     *
*    REGISTER USE:  STANDARD CONVENTIONS.                             *
*                                                                     *
*             R6:  HOLD REG FOR RETURN CODE                           *
*                                                                     *
*    AT ENTRY R1 CONTAINS:                                            *
*             +0   SAFP PARMLIST                                      *
*             +4   WORKAREA FOR EXIT                                  *
*                                                                     *
*    AT EXIT  R15 CONTAINS THE FOLLOWING RETURN CODE(S)               *
*             0    CONTINUE - NORMAL RACF VALIDATION                  *
*             208  DIE NOW  - INVALID ACCESS                          *
*                                                                     *
*    OUTPUT:  NONE                                                    *
*                                                                     *
*    EXTERNAL REFERENCES:  NONE                                       *
*                                                                     *
*    NOTES:  SEE RESTRICTIONS FOR THIS EXIT IN RACF EXITS GUIDE       *
*                                                                     *
*    ABEND CODES:  NONE                                               *
*                                                                     *
*    CHANGE ACTIVITY:                                                 *
*                                                                     *
*    06-APR-98   INITIAL PROGRAM WRITTEN (LRW)                        *
*    29-MAR-01   ADDED CODE FOR DASD ALLOCATE TO HIST PACKS.          *
*                THE WAY IT USED TO WORK ALLOWED USERS TO ALLOCATE    *
*                BUT THEN NOT UPDATE OR DELETE THE DATASET (LRW)      *
*                                                                     *
***********************************************************************
*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
*
ICHRTX00 CSECT
ICHRTX00 AMODE ANY
ICHRTX00 RMODE 24
         USING *,R15
         B     START
         DC    CL8'ICHRTX00',CL1'.',CL8'&SYSDATE',CL1'.',CL8'&SYSTIME'
START    EQU   *
         DROP  R15
         STM   R14,R12,12(R13)     STORE CALLERS REGS
         LR    R12,R15
         USING ICHRTX00,R12        PROGRAM BASE
         USING PSA,0
         LR    R12,R15             COPY FROM EP REG
         L     R9,4(R1)            ADDRESS ICHRTX00 WORK AREA
         USING WORKSAVE,R9         (DON'T DO A GETMAIN, NOT ALLOWED)
         XC    WORKAREA(WORKAREL),WORKAREA CLEAR WORKAREA
         ST    R13,WORKSAVE+4      SET STANDARD LINKAGE
         ST    R9,8(R13)
         LR    R13,R9              ADDRESS SAVE AREA
         DROP  R9                  DROP OLD ADDRESSING
         USING WORKSAVE,R13        ESTABLISH NEW ADDRESSING
*
         L     R9,0(R1)            GET ADDR. OF SAF PARAMETER LIST
         USING SAFP,R9             BASE FOR SAFP PARAMETER LIST
*----------------------------------------------------------------
*
* THIS SECTION TO CHECK DASD ALLOC ADDED 29-MAR-01 LRW
*
*----------------------------------------------------------------
         CLC   =AL2(SAFPDEF),SAFPREQT  RACDEF?
         BNE   CHECK2              IF NOT RACDEF, DROP TO NEXT CHECK
         L     R8,SAFPRACP         OFFSET TO RACDEF PARAMETER LIST
         AR    R8,R9               ADDR. OF RACDEF  PARAMETER LIST.
         LA    R8,0(R8)            PURIFY ADDRESS
         USING RDXPL,R8
         L     R2,RDXENTIT         GET ENTITY, IN THIS CASE, VOLSER
         LA    R2,0(R2)            PURIFY ADDRESS
         DROP  R8
         B     GETUSER
CHECK2   DS    0H
*
*------------------ END OF 29-MAR-01 ADDED CODE -----------------
*
         CLC   =AL2(SAFPAU),SAFPREQT RACHECK?
         BNE   RETURN0             NEITHER RACDEF OR RACHECK? GO AWAY
         L     R8,SAFPRACP         OFFSET TO RACHECK PARAMETER LIST
         AR    R8,R9               ADDR. OF RACHECK PARAMETER LIST.
         LA    R8,0(R8)            PURIFY ADDRESS
         USING ACHKLIST,R8
         TM    ACHKFLG2,ACHKTALT+ACHKTUPD ALTER OR UPDATE?
         BZ    RETURN0             NOTHING ON, LEAVE.
         TM    ACHKFLG1,ACHK31IN   31 BIT MODE?
         BNO   MODE24              GET 24 BIT VOLUME
         L     R2,ACHKVS31         GET 31 BIT VOLUME
         B     GETUSER             GO GET USERID
MODE24   DS    0H
         XR    R2,R2               CLEAR R2
         ICM   R2,B'0111',ACHKOVOL GET VOLUME
         DROP  R8
*
GETUSER  DS    0H
         CLC   =CL4'HIST',0(R2)    VOLUME WE CARE ABOUT?
         BNE   RETURN0             NOT HIST, GO AWAY
         L     R3,PSAAOLD          OUR ASCB
         L     R4,ASCBASXB-ASCB(R3) OUR ASXB
*
         CLC   =CL4'SYSP',ASXBUSER-ASXB(R4) USERID
         BE    RETURN0             IS SYSPROG? OK TO PROCEED
         CLC   =CL4'P390',ASXBUSER-ASXB(R4) USERID
         BE    RETURN0             IS P390 SUPERUSER? OK TO PROCEED
         CLC   =CL7'IBMUSER',ASXBUSER-ASXB(R4) USERID
         BE    RETURN0             IS IBMUSER SUPERUSER? OK TO PROCEED
         B     RET208              KILL EVERY ONE ELSE
*
***********************************************************************
*    SET A RETURN CODE BASED ON PROGRAM FUNCTION                      *
***********************************************************************
RETURN0  SR    R6,R6                   SETUP RETURN CODE = 0
         B     RETURN                  AND RETURN
RET208   LA    R6,208                  SETUP RC=208
         MVC   SAFPRRET,=F'8'          RETURN CODE 8 IN SAFP LIST
         MVC   SAFPRREA,=XL4'0000000C' WITH C REASON CODE
         B     RETURN                  AND RETURN
***********************************************************************
*    STANDARD EXIT LINKAGE                                            *
***********************************************************************
RETURN   EQU   *
         L     R13,4(R13)              RESTORE R13 FROM SAVEAREA
         L     R14,12(R13)             RESTORE R14 FROM OLD SAVEAREA
         LR    R15,R6                  SET RETURN CODE REGISTER
         LM    R0,R12,20(R13)          RESTORE REST OF REGS
         BSM   0,R14                   RESTORE CALLER MODE AND RETURN
         LTORG
WORKAREA DSECT                     150 BYTE AREA
WORKSAVE DS    18F                 SAVE AREA
         DS    CL78                MAX AVAIL WORKAREA
WORKAREL EQU   150                 SHOULD BE 150 (DOCUMENTED MAXIMUM)
         ICHACHKL
         ICHRDXP
         ICHPRCVT
         ICHRDDFL
         ICHSAFP
         CVT    DSECT=YES,LIST=NO
         IHAPSA DSECT=YES,LIST=NO
         IHAASCB DSECT=YES,LIST=NO
         IHAASXB DSECT=YES,LIST=NO
         END
//LKED.SYSIN DD *
         ENTRY     ICHRTX00
         NAME      ICHRTX00(R)
/*
